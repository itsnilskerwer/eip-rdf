# Aim: Return the latest commit of an EIP Document

# This file contains a curated SPARQL query. 
# To use it, copy the entire query, including prefix definitions.
# (Do not copy comments at the top of this file)

PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://ontologies.semanticarts.com/o/gistCore/>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
CONSTRUCT { 
  ?eip gist:latestCommit ?commit . 
  ?commit dc:date ?latestCommitDate . }
WHERE { 
  ?eip a foaf:Document . 
  { SELECT ?eip (MAX(?commitDate) AS ?latestCommitDate)
    WHERE { ?commit a gist:Event ; dcterms:isVersionOf ?eip ; dc:date ?commitDate . }
    GROUP BY ?eip } 
  ?commit a gist:Event ; 
  dcterms:isVersionOf ?eip ; 
  dc:date ?latestCommitDate . 
}
